buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java'
    id 'maven-publish'
    id 'signing'
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'tech.b180.cordaptor'
version = '1.1'

repositories {
    jcenter()
}

shadowJar {
    relocate 'javax.json', 'tech.b180.cordaptor.shaded.javax.json'
    relocate 'org.glassfish.json', 'tech.b180.cordaptor.shaded.org.glassfish.json'

    // do not append "-all", so the generated shadow jar replaces the existing jar
    classifier = ''
}

dependencies {
    implementation "org.glassfish:javax.json:1.1"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from "${projectDir}/etc/no-sources.txt"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from "${projectDir}/etc/no-javadocs.txt"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact shadowJar
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "Common Development and Distribution License 1.1"
                description = "Shaded implementation of javax.json 1.1 into Cordaptor package namespace"
                url = "https://github.com/b180tech/cordaptor"
                licenses {
                    license {
                        name = "CDDL 1.1"
                        url = "https://spdx.org/licenses/CDDL-1.1.html"
                    }
                }
                developers {
                    developer {
                        id = "bond180"
                        name = "Bond180 Limited"
                        email = "devops@bond180.com"
                    }
                }
                scm {
                    connection = "https://github.com/b180tech/cordaptor.git"
                    url = "https://github.com/b180tech/cordaptor"
                }
            }
        }
    }

    repositories {
        maven {
            credentials {
                username project.sonatypeOssUsername
                password project.sonatypeOssPassword
            }

            name = "central"

            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.maven
}
