apply plugin: 'org.jetbrains.kotlin.jvm'

ext.swaggerOutputDir = file("$buildDir/swagger-resources")

task copySwaggerResources(type: Copy) {
    def output = project.ext.swaggerOutputDir
    doFirst {
        println "Copying swagger resources to $output"
        output.mkdirs()
    }
    from project.file("swagger-ui/dist")
    into "$output/swagger"
}

sourceSets.main.output.dir project.ext.swaggerOutputDir, builtBy: copySwaggerResources

dependencies {
    implementation project(":kernel")
    implementation project(":corda-common")

    implementation name: "glassfish-javax-json-$glassfish_json_version-shaded"
    implementation "io.undertow:undertow-core:$undertow_version"

    implementation "org.koin:koin-core:$koin_version"
    implementation "io.reactivex.rxjava3:rxjava:$rxjava3_version"

    compileOnly "net.corda:corda-core:$corda_core_release_version"
    compileOnly "net.corda:corda-serialization:$corda_core_release_version"

    testImplementation "org.apache.logging.log4j:log4j-slf4j-impl:$log4j_version"

    testImplementation "net.corda:corda-core:$corda_core_release_version"
    testImplementation "net.corda:corda-serialization:$corda_core_release_version"
    testImplementation "net.corda:corda-node-driver:$corda_core_release_version"

    testImplementation "org.koin:koin-test:$koin_version"
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
    testImplementation "org.eclipse.jetty:jetty-client:$jetty_version"

    testImplementation "io.mockk:mockk:1.9.3"
}
