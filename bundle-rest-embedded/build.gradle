apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'net.corda.plugins.cordapp'

cordapp {
    targetPlatformVersion 7
    minimumPlatformVersion 6
    workflow {
        // must be kept in sync with koin.properties
        name "cordaptor-embedded-bundle"
        vendor "B180"
        versionId 1
    }
    signing {
        enabled false
    }
}

jar {
    exclude "reference.conf"

    from "${buildDir}/bundled-reference.conf"

    // Otherwise there will be multiple entries for ModuleProvider in /META-INF/services
    // Because java.util.ServiceLoader will notice only one of the entries, not all modules will be loaded
    // Instead, we provide one overriding entry as part of the bundle JAR (see main/resources)
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

dependencies {
    compileOnly project(":kernel")
    compileOnly "org.koin:koin-core:$koin_version"

    compile project(":rest-endpoint")
    compile project(":local-cache")
    compile project(":corda-service")
}


task bundleReferenceConf {
    outputs.file "${buildDir}/bundled-reference.conf"

    def moduleReferenceFilename = 'module-reference.conf'
    def bundledReferenceFilename = 'bundled-reference.conf'

    def bundledProjects = configurations.compile.dependencies.withType(ProjectDependency).collect {
        it.dependencyProject
    }

    def referenceConfFiles = (bundledProjects + project).collect {
        new Tuple(it, it.sourceSets.main.resources.matching { include moduleReferenceFilename }.files.first())
    }

    inputs.files referenceConfFiles.collect { it[1] }

    doFirst {
        println("The following files will be combined:\n${inputs.files.join("\n")}")

        file("${buildDir}/$bundledReferenceFilename").text =
                "# Automatically generated for embedded CorDapp bundle -- do not edit\n" + referenceConfFiles.collect {
                    "# START OF ${it[0].name}/${it[1].name}\n${it[1].text}\n# END OF ${it[0].name}/${it[1].name}"
                }.join("\n\n")
    }
}

jar.dependsOn(bundleReferenceConf)

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = "Cordaptor embedded CorDapp bundle"
                description = "Single-file deployable CorDapp embedding Cordaptor into a Corda node"
            }
        }
    }
}
